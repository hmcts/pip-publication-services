plugins {
  id 'application'
  id 'uk.gov.hmcts.java' version '0.12.67'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.9'
  id 'org.owasp.dependencycheck' version '12.1.3'
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'org.sonarqube' version '7.2.2.6593'
  id 'io.freefair.lombok' version '9.1.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

def configureSourceSet(String name) {
  sourceSets.create(name) { sourceSet ->
    sourceSet.java {
      compileClasspath += sourceSets.main.output + sourceSets.test.output
      runtimeClasspath += sourceSets.main.output + sourceSets.test.output
      srcDir "src/${name}/java"
    }
    sourceSet.resources.srcDir "src/${name}/resources"
  }
}

['smokeTest', 'integrationTest', 'functionalTest'].each { configureSourceSet(it) }

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom functionalTestImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly

  all {
    exclude group: 'commons-logging', 'module': 'commons-logging'
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  toolVersion = '13.0.0'
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def sonarExclusions = ["src/main/java/uk/gov/hmcts/reform/pip/publication/services/config/WebClientConfiguration.java"]

sonarqube {
  properties {
    property "sonar.projectName", "PIP Publication Services"
    property "sonar.projectKey", "pip-publication-services"
    property "sonar.exclusions", sonarExclusions.join(', ')
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
  skipTestGroups  = false
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
  }
}

ext {
  log4JVersion = '2.25.3'
  reformLogging = '6.1.9'
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-json'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'com.azure.spring:spring-cloud-azure-starter-active-directory:6.1.0'
  implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9"
  implementation "com.github.hmcts.java-logging:logging:7.0.0"
  implementation "com.github.hmcts.java-logging:logging-appinsights:${reformLogging}"
  implementation "org.apache.logging.log4j:log4j-api:${log4JVersion}"
  implementation "org.apache.logging.log4j:log4j-to-slf4j:${log4JVersion}"
  implementation 'net.logstash.logback:logstash-logback-encoder:9.0'
  implementation 'uk.gov.service.notify:notifications-java-client:6.0.0-RELEASE'
  implementation('com.github.hmcts:pip-data-models:2.3.30') {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-jpa'
  }
  implementation 'com.opencsv:opencsv:5.12.0'
  implementation 'org.apache.poi:poi-ooxml:5.5.1'
  implementation 'org.redisson:redisson:3.52.0'
  implementation 'com.giffing.bucket4j.spring.boot.starter:bucket4j-spring-boot-starter:0.13.0'
  implementation 'software.amazon.awssdk:s3:2.41.3'
  implementation 'org.apache.commons:commons-lang3:3.20.0'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'com.squareup.okhttp3:mockwebserver:5.3.2'
  testImplementation 'com.squareup.okhttp3:okhttp:5.3.2'
  testImplementation 'com.squareup.okhttp3:okhttp-tls:5.3.2'
  testImplementation 'org.apache.pdfbox:pdfbox:3.0.6'
  testImplementation 'com.redis:testcontainers-redis:2.2.4'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'io.github.hakky54:logcaptor:2.12.1'
  testImplementation 'com.github.hmcts:fortify-client:1.4.10:all'
  functionalTestImplementation 'io.rest-assured:rest-assured'
}

tasks.register('fortifyScan', JavaExec) {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

application {
  mainClass = 'uk.gov.hmcts.reform.pip.publication.services.Application'
}

bootJar {
  getArchiveFileName().set(provider {
    'pip-publication-services.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
